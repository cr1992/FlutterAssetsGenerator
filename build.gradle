buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.commonmark:commonmark:0.21.0'
    }
}

plugins {
    id 'java'
    id "org.jetbrains.intellij" version "1.17.4"
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    // Gradle Changelog Plugin
    id 'org.jetbrains.changelog' version "1.3.1"
}

import org.commonmark.parser.Parser
import org.commonmark.renderer.html.HtmlRenderer

apply plugin: 'org.jetbrains.changelog'

String properties(String key) {
    return project.findProperty(key).toString()
}


group = properties("pluginGroup")
version = properties("pluginVersion")

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    pluginName = properties("pluginName")

    version = properties("platformVersion")
    type = properties("platformType")
    downloadSources = properties("platformDownloadSources").toBoolean()
    updateSinceUntilBuild = true

    // Plugin Dependencies. Uses `platformPlugins` property from the gradle.properties file.

    plugins = properties("platformPlugins").split(',').toList()
}

// Configure Gradle Changelog Plugin - read more: https://github.com/JetBrains/gradle-changelog-plugin
changelog {
    version = properties("pluginVersion")
    path = "${project.projectDir}/CHANGELOG.md"
//    header = "[${-> version.get()}] - ${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}"
//    headerParserRegex = ~/(\d+\.\d+)/
//    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "[Unreleased]"
    groups = ["Added", "Changed", "Deprecated", "Removed", "Fixed", "Security"]
}

runIde {
    // Absolute path to installed target 3.5 Android Studio to use as
    // IDE Development Instance (the "Contents" directory is macOS specific):
//    ideDir.set(file("/Applications/Android Studio.app/Contents"))
}

patchPluginXml {
    version = properties("pluginVersion")
    sinceBuild = properties("pluginSinceBuild")
    untilBuild = properties("pluginUntilBuild")
    
    // Convert README to HTML using CommonMark
    pluginDescription = provider {
        def parser = Parser.builder().build()
        def renderer = HtmlRenderer.builder().build()
        def document = parser.parse(project.file("README.md").text)
        renderer.render(document)
    }
    
    // Get the latest available change notes from the changelog file
    changeNotes = provider {
        changelog.getOrNull("3.0.0")?.toHTML() ?: changelog.getUnreleased().toHTML()
    }
}

runPluginVerifier {
    ideVersions = properties("pluginVerifierIdeVersions").split(',').toList()
}